<!DOCTYPE html>
<html data-theme='dark'>
  <head>
    <title>Snake</title>
    <link rel="icon" href="data:image/svg+xml,<svg xmlns=%22http://www.w3.org/2000/svg%22 viewBox=%220 0 100 100%22><text y=%22.9em%22 font-size=%2290%22>üêç</text></svg>">
    <meta name="viewport" content="width=device-width, initial-scale=1, user-scalable=no" />

    <!-- Docs: https://hyperscript.org/docs -->
    <script src="//unpkg.com/hyperscript.org@0.9.12"></script>

    <!-- Docs: https://daisyui.com/components/ -->
    <link href="//cdn.jsdelivr.net/npm/daisyui@4.6.2/dist/full.min.css" rel="stylesheet" type="text/css" />
    <script src="//cdn.tailwindcss.com"></script>
    <style>
      @media (hover: none) { 
        #controls {
          display: grid;
        }
      }

      [data-cell='lightning'] {
        color: yellow;
      }

      [data-cell='snake'][data-direction='vertical'] {
        transform: rotate(90deg);
      }

      [data-score]::after {
        content: attr(data-score);
      }
      #controls {
        transition: opacity 400ms ease;
      }
      #grid {
        transition: opacity 400ms ease;
      }
      @keyframes spin { 100% { transform: rotate(360deg) } }
      #loading {
          animation: spin 2s infinite;
      }
    </style>
    <script type="text/hyperscript">
      behavior Repeat(amount)
        init 
          repeat amount times index i
            call my.content.cloneNode(true)
            set fragment to the result 
            put i into fragment.firstElementChild@data-index
            put fragment before me
          end
        end
      end
      behavior SnakeButton(direction)
        on touchstart
          steer(direction)
          wait for touchcancel or touchend or 400ms
          if the result is a Number
            set $snake.button_hold to true
          end
        end

        on touchcancel or touchend
          set $snake.button_hold to false
        end
      end

      def snakeIcon(direction)
        if direction == 'up' 
          return '‚ñ≤'
        else if direction == 'down' 
          return '‚ñº'
        else if direction === 'right' 
          return '‚ñ∂'
        else if direction === 'left'
          return '‚óÄ'
        else 
          return 'idk'
        end
      end

      def move()
        if $time is not null
          set $time.elapsed to 0
        end

        // convert head into body
        set head to the <[data-cell='snake-head']/> in me
        tell head
          set your@data-cell to 'snake-body'
          set your@data-snake-body to 0
          if your@data-direction is 'up' or your@data-direction is 'down'
            set your@data-direction to 'vertical'
          end
        end

        // update body
        for segment in <[data-cell=snake-body]/> 
          tell segment
            increment your@data-snake-body
            if your@data-snake-body > $snake.body_size
              add @data-cell=empty to you
              remove @data-snake-body from you
            end
          end
        end

        // move head
        set x to head's @data-x as Number
        set y to head's @data-y as Number

        if $snake.direction == 'right' 
          increment x
          if x >= 10 
            set x to 0
          end
        else if $snake.direction == 'left' 
          decrement x
          if x < 0
            set x to 9
          end
        else if $snake.direction == 'down' 
          increment y
          if y >= 10
            set y to 0
          end
        else if $snake.direction == 'up' 
          decrement y 
          if y < 0
            set y to 9
          end
        end
        set cell_id to `cell-${x}-${y}`
        tell #{cell_id} in me
          if your @data-cell is not 'food' and your @data-cell is not 'empty'
            send snake:collision to the closest <body />
          end
          set your@data-cell to 'snake-head'
          set your@data-direction to $snake.direction
        end
      end

      def steer(direction)
        if $snake.direction is direction
          move()
        else
          set $snake.direction to direction
          put direction into <[data-snake-body='0']/>'s @data-direction
        end
      end

      def spawn_hazard()
        set hazard to random in ['lightning', 'fire']
        spawn(hazard)
      end

      def spawn(item)
        get the <[data-cell='empty']/> in me
        if the result's length is 0
          alert('Grid is maxed out')
        else
          set el to random in result
          put item into el's @data-cell 
        end
      end

      def stop_animation_loop()
        set $time to null
      end

      def start_animation_loop()
        set $time to { previous: null, elapsed: 0 }
        call requestAnimationFrame(animate)
      end

      def animate(time)
        // time is stopped
        if $time is null 
          exit
        end

        // we need one empty tick of the clock to calibrate elapsed time
        if $time.previous is null
          set $time.previous to time
          call requestAnimationFrame(animate)
          exit
        end

        set $delta to time - $time.previous
        set $time.elapsed to $time.elapsed + $delta
        set $time.previous to time

        // the snake moves every 100ms minus 20ms for each speed level
        set threshold to 1000 - ($snake.speed * 20)

        if $snake.button_hold and 85 < threshold
          set threshold to 85
        end

        if $time.elapsed > threshold
          move()
        end
        
        call requestAnimationFrame(animate)
      end
     
    </script>

    <!-- Docs: https://tailwindcss.com/ -->
  </head>

  <body 
    autofocus 
    tabindex="-1"
    _="
      on snake:init
        -- reset dom
        add .hidden to <#play-again /> in me
        add .hidden to <#loading /> in me
        remove @disabled from <#controls button/> in me
        remove .opacity-0 from <#controls/> in me
        remove .opacity-0 from <#grid/> in me

        add @data-cell=empty to <[data-cell]/> in me
        set <[data-score]/>'s @data-score to 0

        -- set snake position
        get the #cell-0-0 in me
        add @data-cell='snake-head' to it
        add @data-direction='right' to it

        -- set food position
        spawn('food')

        -- set globals
        set $snake to { direction: 'right', speed: 0, body_size: 0 }
        start_animation_loop()
      end

      on snake:consume
        increment $snake.speed
        increment $snake.body_size
        increment <[data-score]/>'s @data-score
        spawn('food')
        spawn_hazard()
      end

      on snake:collision
        stop_animation_loop()
        alert('game over')
        add @disabled to <#controls button/>
        remove .hidden from #play-again
      end

      on exception(error)
        log 'exception', error
      end

      on keydown[key=='h'] steer('left') end
      on keydown[key=='l'] steer('right') end
      on keydown[key=='j'] steer('down') end
      on keydown[key=='k'] steer('up') end

      on keydown[key=='ArrowLeft'] steer('left') end
      on keydown[key=='ArrowRight'] steer('right') end
      on keydown[key=='ArrowDown'] steer('down') end
      on keydown[key=='ArrowUp'] steer('up') end

      on keydown[key=='w'] steer('up') end
      on keydown[key=='a'] steer('left') end
      on keydown[key=='s'] steer('down') end
      on keydown[key=='d'] steer('right') end

      on keydown[key=='Enter'] 
        if $time
          stop_animation_loop()
        else
          start_animation_loop()
        end
      end
    "
  >
    <main class="flex flex-col items-center gap-4 mt-4"> 
      <div>
        Score:
        <span data-score='0'></span>
      </div>
      <div 
        id="loading" 
        class="flex items-center justify-center text-7xl"
        style="height: 311px; width: 311px"
      >
        üêç
      </div>
      <table id="grid" class="opacity-0 border-spacing-0 border-collapse">
        <tbody>
          <template _="install Repeat(amount: 10)">
            <tr class="border border-neutral">
              <template _="install Repeat(amount: 10)">
                <td class="border border-neutral">
                  <span class="flex h-7 w-7 items-center justify-center">
                    <pre
                      data-cell='empty'
                      class="text-lg"
                      _="
                        init
                          get the closest <td/> then put its@data-index into @data-x
                          get the closest <tr/> then put its@data-index into @data-y
                          put `cell-${@data-x}-${@data-y}` into @id

                          if @data-x == 9 and @data-y == 9 
                            send snake:init to the closest <body />
                          end
                        end

                        on mutation of attributes
                          set old_cell to event.detail.mutationList[0].oldValue
                          if old_cell == 'food'
                            if @data-cell == 'snake-head'
                              send snake:consume to the closest <body />
                            end
                          end

                          if @data-cell == 'empty' 
                            put ' ' into me
                          else if @data-cell == 'food'
                            put '$' into me
                          else if @data-cell == 'fire'
                            put 'üî•' into me
                          else if @data-cell == 'lightning'
                            put '‚ö°' into me
                          else if @data-cell == 'snake-head' 
                            put snakeIcon(@data-direction) into me
                          else if @data-cell == 'snake-body'
                            put '=' into me
                          end
                        end
                      "
                    >
                    </pre>
                  </span>
                </td>
              </template>
            </tr>
          </template>
        </tbody>
      </table>
      <button 
        id="play-again" 
        class="hidden btn btn-accent"
        _='on click send snake:init to closest <body/>'
      >
        Play again?
      </button>
      <section id="controls" class="hidden opacity-0 grid-cols-3">
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'up')">‚ñ≤</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'left')">‚óÄ</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'right')">‚ñ∂</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'down')">‚ñº</button>
        <div class=""></div>
      </section>
    </main>
  </body>
</html>
