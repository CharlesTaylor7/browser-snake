<!DOCTYPE html>
<html data-theme='dark'>
  <head>
    <title>Snake</title>
    <link rel="icon" href="data:image/svg+xml,<svg xmlns=%22http://www.w3.org/2000/svg%22 viewBox=%220 0 100 100%22><text y=%22.9em%22 font-size=%2290%22>&#128013;</text></svg>">
    <meta name="viewport" content="width=device-width, initial-scale=1" />

    <!-- Docs: https://hyperscript.org/docs -->
    <script src="//unpkg.com/hyperscript.org@0.9.12"></script>

    <!-- Docs: https://daisyui.com/components/ -->
    <link href="//cdn.jsdelivr.net/npm/daisyui@4.6.2/dist/full.min.css" rel="stylesheet" type="text/css" />
    <script src="//cdn.tailwindcss.com"></script>

    <script type="text/hyperscript">
      behavior Repeat(amount)
        init 
          repeat amount times index i
            call my.content.cloneNode(true)
            set fragment to the result 
            put i into fragment.firstElementChild@data-index
            put fragment before me
          end
        end
      end

      def snakeIcon(direction)
        if direction == 'up' 
          return '▲'
        else if direction == 'down' 
          return '▼'
        else if direction === 'right' 
          return '▶'
        else if direction === 'left'
          return '◀'
        else 
          return 'idk'
        end
      end

      def randomizeFood()
        get the <[data-cell='empty']/> in me
        if the result's length > 0
          set el to random in result
          put 'food' into el's @data-cell 
        else
          alert('Grid is maxed out, cannot spawn new food, good job!')
        end
      end

      def move()
        set $time.elapsed to 0
        get <[data-cell=snake]/> in me 
        call result.sort(\a, b -> a's @data-snake-index as Number - b's @data-snake-index as Number)
        set snake to the result

        // update body
        for segment in snake
          increment its@data-snake-index
        end
        if @data-grow-snake
          remove @data-grow-snake
        else
          add @data-cell='empty' to the last snake
          remove @data-snake-index from the last snake
        end

        // move head
        set head to the first snake
        set x to head's @data-x as Number
        set y to head's @data-y as Number
        if head@data-direction == 'right' 
          increment x
          if x >= 10 
            set x to 0
          end
        else if head@data-direction == 'left' 
          decrement x
          if x < 0
            set x to 9
          end
        else if head@data-direction == 'down' 
          increment y
          if y >= 10
            set y to 0
          end
        else if head@data-direction == 'up' 
          decrement y 
          if y < 0
            set y to 9
          end
        end

        set cell_id to `cell-${x}-${y}`
        get the #{cell_id} in me

        if the result's @data-cell == 'snake'
          send snake:collision to the closest <body />
        end

        set its@data-cell to 'snake'
        set its@data-snake-index to '0'
        set its@data-direction to @data-direction
      end

      def steer(direction)
        if @data-direction is direction
          move()
        else
          set @data-direction to direction
          set the @data-direction of the <[data-snake-index='0'] /> to direction
        end
      end

      def animate(time)
        if not @data-paused
          get $time.elapsed as Number
          set $time.elapsed to it + (time - $time.previous)
        end

        set $time.previous to time

        // the snake moves every 750ms minus 20ms for each speed level
        if $time.elapsed > (750 - (@data-speed * 20))
          move()
        end

        call requestAnimationFrame(animate)
      end
     
    </script>

    <!-- Docs: https://tailwindcss.com/ -->
  </head>

  <body 
    autofocus 
    tabindex="-1"
    data-speed="0"
    data-score="0"
    data-direction='right'
    _="
      on snake:init
        randomizeFood()
        set $time to { elapsed: 0, previous: 0 }
        call requestAnimationFrame(animate)
      end

      on snake:consume
        increment @data-speed
        increment @data-score
        add @data-grow-snake
        randomizeFood()
      end

      on snake:collision
        add @data-paused
        alert('game over')
      end

      on mutation[target==me] of @data-score
        put @data-score into the #score
      end

      on keydown[key=='h'] steer('left') end
      on keydown[key=='l'] steer('right') end
      on keydown[key=='j'] steer('down') end
      on keydown[key=='k'] steer('up') end

      on keydown[key=='ArrowLeft'] steer('left') end
      on keydown[key=='ArrowRight'] steer('right') end
      on keydown[key=='ArrowDown'] steer('down') end
      on keydown[key=='ArrowUp'] steer('up') end

      on keydown[key=='w'] steer('up') end
      on keydown[key=='a'] steer('left') end
      on keydown[key=='s'] steer('down') end
      on keydown[key=='d'] steer('right') end

      on keydown[key=='Enter']
        toggle @data-paused
      end
    "
  >
    <main class="flex flex-col items-center gap-4 mt-4"> 
      <div>
        Score:
        <span id="score">0</span>
      </div>
      <table class="border-spacing-0 border-collapse">
        <tbody>
          <template _="install Repeat(amount: 10)">
            <tr class="border border-neutral">
              <template _="install Repeat(amount: 10)">
                <td class="border border-neutral">
                  <span class="flex h-7 w-7 items-center justify-center">
                    <pre
                      data-cell='empty'
                      class="text-lg"
                      _="
                        init
                          get the closest <td/> 
                          set @data-x to its@data-index
                          get the closest <tr/>
                          set @data-y to its@data-index
                          put `cell-${@data-x}-${@data-y}` into my@id

                          if @data-x == 0 and @data-y == 0 
                            put 'snake' into @data-cell
                            put '0' into @data-snake-index
                            put 'right' into @data-direction
                          end

                          if @data-x == 9 and @data-y == 9 
                            send snake:init to the closest <body />
                          end
                        end

                        on mutation of attributes
                          if event.detail.mutationList[0].attributeName == 'class'
                            exit
                          end
                          set old_cell to event.detail.mutationList[0].oldValue
                          if old_cell == 'food'
                            if @data-snake-index == '0'
                              send snake:consume to the closest <body />
                            else
                              log `error: something besides the snake head consumed food`
                              log @data-cell
                            end
                          end
                          if 
                            @data-cell == 'snake' and 
                            @data-snake-index != '0' and 
                            (@data-direction == 'up' or @data-direction == 'down')
                            add .rotate-90
                          else
                            remove .rotate-90
                          end

                          if @data-cell == 'empty' 
                            put ' ' into me
                          else if @data-cell == 'food'
                            put '$' into me
                          else if @data-cell == 'snake' 
                            if @data-snake-index == '0'
                              put snakeIcon(@data-direction) into me
                            else
                              put '=' into me
                            end
                          end
                        end
                      "
                    >
                    </pre>
                  </span>
                </td>
              </template>
            </tr>
          </template>
        </tbody>
      </table>
      <section class="grid grid-cols-3" id="controls">
        <div class=""></div>
        <button class="btn btn-lg btn-primary" _="on click steer('up')">▲</button>
        <div class=""></div>
        <button class="btn btn-lg btn-primary" _="on click steer('left')">◀</button>
        <div class=""></div>
        <button class="btn btn-lg btn-primary" _="on click steer('right')">▶</button>
        <div class=""></div>
        <button class="btn btn-lg btn-primary" _="on click steer('down')">▼</button>
        <div class=""></div>
      </section>
    </main>
  </body>
</html>
