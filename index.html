<!DOCTYPE html>
<html>
  <head>
    <title>Snake</title>

    <!-- Docs: https://hyperscript.org/docs -->
    <script src="//unpkg.com/hyperscript.org@0.9.12"></script>
    <script src="//cdn.tailwindcss.com"></script>

    <script type="text/hyperscript">
      behavior Repeat(amount)
        init 
          repeat amount times index i
            call my.content.cloneNode(true)
            set fragment to the result 
            put i into fragment.firstElementChild@data-index
            put fragment before me
          end
        end
      end

      def snakeIcon(direction)
        if direction == 'up' 
          return '▲'
        else if direction == 'down' 
          return '▼'
        else if direction === 'right' 
          return '▶'
        else if direction === 'left'
          return '◀'
        else 
          return 'idk'
        end
      end

      def randomizeFood()
        get the <[data-cell='empty']/> in me
        if the result's length > 0
          set el to random in result
          put 'food' into el's @data-cell 
        else
          alert('Grid is maxed out, cannot spawn new food, good job!')
        end
      end

      def move()
        log 'move'
        set $time.elapsed to 0
        get <[data-cell='snake-head']/> in me then set head to the result
        set x to head's @data-x as Number
        set y to head's @data-y as Number
        if @data-direction == 'right' 
          increment x
          if x >= 10 
            set x to 0
          end
        else if @data-direction == 'left' 
          decrement x
          if x < 0
            set x to 9
          end
        else if @data-direction == 'down' 
          increment y
          if y >= 10
            set y to 0
          end
        else if @data-direction == 'up' 
          decrement y 
          if y < 0
            set y to 9
          end
        end

        set head@data-cell to 'empty'
        log 'x', x, 'y', y
        set cell_id to `cell-${x}-${y}`
        get the #{cell_id} in me
        set its@data-cell to 'snake-head'
        set its@data-direction to @data-direction
      end

      def animate(time)
        if not @data-paused
          get $time.elapsed as Number
          set $time.elapsed to it + (time - $time.previous)
        end

        set $time.previous to time

        // the snake moves every second minus 10ms for each speed level
        if $time.elapsed > (1000 - (@data-speed * 10))
          move()
        end

        call requestAnimationFrame(animate)
      end
     
    </script>

    <!-- Docs: https://tailwindcss.com/ -->
  </head>

  <body 
    autofocus 
    tabindex="-1"
    data-direction="right"
    data-speed="0"
    _="
      on snake:init
        randomizeFood()
        set $time to { elapsed: 0, previous: 0 }
        call requestAnimationFrame(animate)
      end

      on mutation[target==me] of @data-direction
        get the <[data-cell='snake-head']/> in me
        put @data-direction into its@data-direction
        log 'change dir'
      end

      on keydown[key=='h'] 
        set @data-direction to 'left'
      end

      on keydown[key=='l'] 
        set @data-direction to 'right'
      end

      on keydown[key=='j'] 
        set @data-direction to 'down'
      end

      on keydown[key=='k'] 
        set @data-direction to 'up'
      end

      on keydown[key==' ']
        move()
      end

      on keydown[key=='Enter']
        toggle @data-paused
        if @data-paused
          log 'paused'
        else 
          log 'unpaused'
        end
      end
    "
  >
    <main class="flex flex-col items-center gap-4 mt-4"> 
      <div>
        Score:
        <span id="score"></span>
      </div>
      <table class="border-spacing-0 border-collapse">
        <tbody>
          <template _="install Repeat(amount: 10)">
            <tr class="border border-neutral">
              <template _="install Repeat(amount: 10)">
                <td class="border border-neutral">
                  <span class="flex h-7 w-7 items-center justify-center">
                    <!--style="content: attr(data-cell)"-->
                    <pre
                      data-cell='empty'
                      data-direction='right'
                      class="text-lg"
                      _="
                        init
                          get the closest <td/> 
                          set @data-x to its@data-index
                          get the closest <tr/>
                          set @data-y to its@data-index
                          put `cell-${@data-x}-${@data-y}` into my@id

                          if @data-x == 0 and @data-y == 0 
                            put 'snake-head' into @data-cell
                          end
                          if @data-x == 9 and @data-y == 9 
                            send snake:init to the closest <body />
                          end
                        end

                        on mutation of attributes
                          log event
                          if @data-cell == 'empty' 
                            put ' ' into me
                          else if @data-cell == 'food'
                            put '$' into me
                          else if @data-cell == 'snake-body'
                            put '=' into me
                            if @data-direction == 'up' or @data-direction == 'down'
                              add .rotate-90
                            else
                              remove .rotate-90
                            end
                          else if @data-cell == 'snake-head'
                            put snakeIcon(@data-direction) into me
                          end
                        end
                      "
                    >
                    </pre>
                  </span>
                </td>
              </template>
            </tr>
          </template>
        </tbody>
      </table>
    </main>
  </body>
</html>
