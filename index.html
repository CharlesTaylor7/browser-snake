<!DOCTYPE html>
<html data-theme='dark'>
  <head>
    <title>Snake</title>
    <link rel="icon" href="data:image/svg+xml,<svg xmlns=%22http://www.w3.org/2000/svg%22 viewBox=%220 0 100 100%22><text y=%22.9em%22 font-size=%2290%22>üêç</text></svg>">
    <meta name="viewport" content="width=device-width, initial-scale=1, user-scalable=no" />

    <!-- Docs: https://hyperscript.org/docs -->
    <script src="//unpkg.com/hyperscript.org@0.9.12"></script>

    <!-- Docs: https://daisyui.com/components/ -->
    <link href="//cdn.jsdelivr.net/npm/daisyui@4.6.2/dist/full.min.css" rel="stylesheet" type="text/css" />
    <script src="//cdn.tailwindcss.com"></script>
    <style>
      @media (hover: none) { 
        #controls {
          display: grid;
        }
      }
      #controls {
        transition: opacity 2s ease;
      }
      #grid {
        transition: opacity 2s ease;
      }
      @keyframes spin { 100% { transform: rotate(360deg) } }
      #loading {
          animation: spin 2s infinite;
      }
    </style>
    <script type="text/hyperscript">
      behavior Repeat(amount)
        init 
          repeat amount times index i
            call my.content.cloneNode(true)
            set fragment to the result 
            put i into fragment.firstElementChild@data-index
            put fragment before me
          end
        end
      end
      behavior SnakeButton(direction)
        on touchstart
          steer(direction)
          wait for touchcancel or touchend or 400ms
          if the result is a Number
            add @data-button-hold to closest <body />
          end
        end

        on touchcancel or touchend
          remove @data-button-hold from closest <body />
        end
      end

      def snakeIcon(direction)
        if direction == 'up' 
          return '‚ñ≤'
        else if direction == 'down' 
          return '‚ñº'
        else if direction === 'right' 
          return '‚ñ∂'
        else if direction === 'left'
          return '‚óÄ'
        else 
          return 'idk'
        end
      end

      def randomizeFood()
        get the <[data-cell='empty']/> in me
        if the result's length > 0
          set el to random in result
          put 'food' into el's @data-cell 
        else
          alert('Grid is maxed out, cannot spawn new food, good job!')
        end
      end

      def move()
        set $time.elapsed to 0
        get <[data-cell=snake]/> in me 
        call result.sort(\a, b -> a's @data-snake-index as Number - b's @data-snake-index as Number)
        set snake to the result

        // update body
        for segment in snake
          increment its@data-snake-index
        end
        if @data-grow-snake
          remove @data-grow-snake
        else
          add @data-cell='empty' to the last snake
          remove @data-snake-index from the last snake
        end

        // move head
        set head to the first snake
        set x to head's @data-x as Number
        set y to head's @data-y as Number
        if head@data-direction == 'right' 
          increment x
          if x >= 10 
            set x to 0
          end
        else if head@data-direction == 'left' 
          decrement x
          if x < 0
            set x to 9
          end
        else if head@data-direction == 'down' 
          increment y
          if y >= 10
            set y to 0
          end
        else if head@data-direction == 'up' 
          decrement y 
          if y < 0
            set y to 9
          end
        end

        set cell_id to `cell-${x}-${y}`
        get the #{cell_id} in me

        if the result's @data-cell == 'snake'
          send snake:collision to the closest <body />
        end

        set its@data-cell to 'snake'
        set its@data-snake-index to '0'
        set its@data-direction to @data-direction
      end

      def steer(direction)
        if @data-direction is direction
          move()
        else
          set @data-direction to direction
          set the @data-direction of the <[data-snake-index='0'] /> to direction
        end
      end

      def animate(time)
        if $time is null 
          set $time to { previous: time, elapsed: 0 }
        end
        if $time.paused
          exit
        end

        set $delta to time - $time.previous
        set $time.elapsed to $time.elapsed + $delta
        set $time.previous to time

        // the snake moves every 100ms minus 20ms for each speed level
        set threshold to 1000 - (@data-speed * 20)

        if @data-button-hold and 85 < threshold
          set threshold to 85
        end

        if $time.elapsed > threshold
          move()
        end
        
        call requestAnimationFrame(animate)
      end
     
    </script>

    <!-- Docs: https://tailwindcss.com/ -->
  </head>

  <body 
    autofocus 
    tabindex="-1"
    _="
      on snake:init
        wait 2s
        -- reset dom
        add .hidden to <#play-again /> in me
        add .hidden to <#loading /> in me
        remove @disabled from <#controls button/> in me
        remove .opacity-0 from <#controls/> in me
        remove .opacity-0 from <#grid/> in me

        add @data-cell=empty to <[data-cell]/> in me

        -- set snake position
        get the #cell-0-0 in me
        add @data-cell=snake to it
        add @data-snake-index='0' to it
        add @data-direction='right' to it

        -- set food position
        randomizeFood()

        -- set globals
        add @data-direction='right'
        add @data-speed='0'
        add @data-score='0'
        call requestAnimationFrame(animate)
      end

      on snake:consume
        increment @data-speed
        increment @data-score
        add @data-grow-snake
        randomizeFood()
      end

      on snake:collision
        set $time.paused to true
        alert('game over')
        add @disabled to <#controls button/>
        make <button#play-again.btn.btn-accent />
        put 'Play again?' into it
        add @_='on click send snake:init to closest <body/>' to it
        put it before #controls
      end

      on mutation[target==me] of @data-score
        put @data-score into the #score
      end

      on keydown[key=='h'] steer('left') end
      on keydown[key=='l'] steer('right') end
      on keydown[key=='j'] steer('down') end
      on keydown[key=='k'] steer('up') end

      on keydown[key=='ArrowLeft'] steer('left') end
      on keydown[key=='ArrowRight'] steer('right') end
      on keydown[key=='ArrowDown'] steer('down') end
      on keydown[key=='ArrowUp'] steer('up') end

      on keydown[key=='w'] steer('up') end
      on keydown[key=='a'] steer('left') end
      on keydown[key=='s'] steer('down') end
      on keydown[key=='d'] steer('right') end

      on keydown[key=='Enter'] 
        set $time.paused to not $time.paused 
        if not $time.paused
      end
    "
  >
    <main class="flex flex-col items-center gap-4 mt-4"> 
      <div>
        Score:
        <span id="score">0</span>
      </div>
      <div 
        id="loading" 
        class="flex items-center justify-center text-5xl"
        style="height: 311px; width: 311px"
      >
        üêç
      </div>
      <table id="grid" class="opacity-0 border-spacing-0 border-collapse">
        <tbody>
          <template _="install Repeat(amount: 10)">
            <tr class="border border-neutral">
              <template _="install Repeat(amount: 10)">
                <td class="border border-neutral">
                  <span class="flex h-7 w-7 items-center justify-center">
                    <pre
                      data-cell='empty'
                      class="text-lg"
                      _="
                        init
                          get the closest <td/> 
                          set @data-x to its@data-index
                          get the closest <tr/>
                          set @data-y to its@data-index
                          put `cell-${@data-x}-${@data-y}` into my@id

                          if @data-x == 9 and @data-y == 9 
                            send snake:init to the closest <body />
                          end
                        end

                        on mutation of attributes
                          if event.detail.mutationList[0].attributeName == 'class'
                            exit
                          end
                          set old_cell to event.detail.mutationList[0].oldValue
                          if old_cell == 'food'
                            if @data-snake-index == '0'
                              send snake:consume to the closest <body />
                            end
                          end
                          if 
                            @data-cell == 'snake' and 
                            @data-snake-index != '0' and 
                            (@data-direction == 'up' or @data-direction == 'down')
                            add .rotate-90
                          else
                            remove .rotate-90
                          end

                          if @data-cell == 'empty' 
                            put ' ' into me
                          else if @data-cell == 'food'
                            put '$' into me
                          else if @data-cell == 'snake' 
                            if @data-snake-index == '0'
                              put snakeIcon(@data-direction) into me
                            else
                              put '=' into me
                            end
                          end
                        end
                      "
                    >
                    </pre>
                  </span>
                </td>
              </template>
            </tr>
          </template>
        </tbody>
      </table>
      <button 
        id="play-again" 
        class="hidden btn btn-accent"
        _='on click send snake:init to closest <body/>'
      >
        Play again?
      </button>
      <section id="controls" class="hidden opacity-0 grid-cols-3">
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'up')">‚ñ≤</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'left')">‚óÄ</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'right')">‚ñ∂</button>
        <div class=""></div>
        <button class="btn h-20 w-20 p-0 btn-primary text-4xl" _="install SnakeButton(direction: 'down')">‚ñº</button>
        <div class=""></div>
      </section>
    </main>
  </body>
</html>
